plugins {
    id "io.spring.dependency-management" version "1.0.14.RELEASE"
    id 'com.github.johnrengelman.shadow' version '7.1.2' apply false
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:2.7.2"
                mavenBom "org.testcontainers:testcontainers-bom:1.17.3"
                mavenBom "com.google.protobuf:protobuf-bom:3.21.1"
            }
            dependency "com.google.guava:guava:${Versions.guava}"
            dependency "org.ow2.asm:asm-commons:${Versions.asm}"
            dependency "org.glassfish:jakarta.json:${Versions.glassfishJson}"

            dependency "org.projectlombok:lombok:${Versions.lombok}"
            dependency "com.google.code.gson:gson:${Versions.gson}"

            dependency "org.webjars:sockjs-client:${Versions.sockjs}"
            dependency "org.webjars:stomp-websocket:${Versions.stomp}"
            dependency "org.webjars:bootstrap:${Versions.bootstrap}"
            dependency "io.r2dbc:r2dbc-postgresql:${Versions.r2dbcPostgresql}"
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()

            force("com.google.errorprone:error_prone_annotations:2.7.1")
        }
    }
}

task managedVersions {
    doLast {
        dependencyManagement.managedVersions.each {
            println it
        }
    }
}
